?start: (import_stmt | globalvar | procedure_def)*
?start_header: (import_stmt | globalvar_decl | procedure_decl)*

// core language

globalvar: EXPORT? VAR IDENTIFIER COLON type EQUALS (expression | array_def) SEMICOLON

globalvar_decl: EXTERN VAR IDENTIFIER COLON type SEMICOLON

import_stmt: IMPORT string SEMICOLON

procedure_decl: EXTERN procedure_prototype

procedure_def: EXPORT? procedure_prototype procedure_def_vars? BEGIN block END

procedure_prototype: PROCEDURE IDENTIFIER LPAREN procedure_def_args RPAREN COLON IDENTIFIER SEMICOLON
procedure_def_args: procedure_def_arg (COMMA procedure_def_arg)*
procedure_def_arg: IDENTIFIER COLON type

procedure_def_vars: VARS procedure_def_var*
procedure_def_var: IDENTIFIER COLON type SEMICOLON

type: IDENTIFIER | POINTER TO IDENTIFIER | ARRAY LBRACKET NUMBER RBRACKET OF IDENTIFIER

array_def: LBRACKET ( | (expression (COMMA expression)*)) RBRACKET

pointer_index: IDENTIFIER LBRACKET expression RBRACKET

// based on: https://en.cppreference.com/w/c/language/operator_precedence.html
expression: logical_or

logical_or: logical_and | logical_or LOGICAL_OR logical_and

logical_and: bitor | logical_and LOGICAL_AND bitor

bitor: bitxor | bitor BW_OR bitxor

bitxor: bitand | bitxor BW_XOR bitand

bitand: equality | bitand BW_AND equality

equality: compare | equality CMP_EQ compare | equality CMP_NEQ compare

compare: bitshift | compare CMP_LT bitshift | compare CMP_GT bitshift | compare CMP_LTEQ bitshift | compare CMP_GTEQ bitshift

bitshift: sum | bitshift BW_SHIFT_LEFT sum | bitshift BW_SHIFT_RIGHT sum

sum: product | sum PLUS product | sum MINUS product

product: unary | product MULTIPLY unary | product DIVIDE unary | product REMAINDER unary

unary: BW_NOT unary | atom

atom: IDENTIFIER | SIGNED_NUMBER | pointer_index | procedure_call | LPAREN expression RPAREN | ESCAPED_STRING

procedure_call: IDENTIFIER LPAREN (expression (COMMA expression)*)* RPAREN

assignment: IDENTIFIER EQUALS expression SEMICOLON

return: RETURN expression SEMICOLON

while: WHILE expression DO block DONE

if: IF expression THEN block (ELSIF expression THEN block)* (ELSE block)? END

statement: assignment | return | while | if

block: statement*

// keywords and terms and stuff

// keywords
IMPORT: "IMPORT"
EXTERN: "EXTERN"
EXPORT: "EXPORT"
VAR: "VAR"
PROCEDURE: "PROCEDURE"
VARS: "VARS"
BEGIN: "BEGIN"
END: "END"
WHILE: "WHILE"
DO: "DO"
DONE: "DONE"
IF: "IF"
THEN: "THEN"
ELSIF: "ELSIF"
ELSE: "ELSE"
RETURN: "RETURN"
POINTER: "POINTER"
TO: "TO"
ARRAY: "ARRAY"
OF: "OF"

// simple terms
SEMICOLON: ";"
COLON: ":"
COMMA: ","
LPAREN: "("
RPAREN: ")"
LBRACKET: "["
RBRACKET: "]"
EQUALS: "="
PLUS: "+"
MINUS: "-"
MULTIPLY: "*"
DIVIDE: "/"
REMAINDER: "%"
BW_SHIFT_LEFT: "<<"
BW_SHIFT_RIGHT: ">>"
BW_AND: "&"
BW_NOT: "~"
BW_OR: "|"
BW_XOR: "^"
CMP_LT: "<"
CMP_GT: ">"
CMP_LTEQ: "<="
CMP_GTEQ: ">="
CMP_EQ: "=="
CMP_NEQ: "!="
LOGICAL_AND: "&&"
LOGICAL_OR: "||"

// other stuff, no idea what u call this
IDENTIFIER: /[a-zA-Z_][a-zA-Z0-9_]*/
string: ESCAPED_STRING

// imports etc.

%import common.ESCAPED_STRING
%import common.SIGNED_NUMBER
%import common.NUMBER
%import common.WS

%ignore WS
