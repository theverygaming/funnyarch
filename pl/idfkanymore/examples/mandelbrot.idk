// based on the assembly mandelbrot example

EXPORT VAR framebuffer: POINTER TO U32 = 0xF0000000;

EXPORT PROCEDURE main(): VOID;
BEGIN
    mandelbrot();
END

EXPORT PROCEDURE mandelbrot(): VOID;
VARS
    px: U32;
    py: U32;
    cx: U32;
    cy: U32;
    pix32: U32;
    _32: U8;
    x: U32;
    y: U32;
    x_2: U32;
    y_2: U32;
    iteration: USIZE;
BEGIN
    py = 0;
    cy = 0xffffc000;
    WHILE py < 480 DO
        cx = 0xffff7000;

        // iterate over 4-byte words
        px = 0;
        WHILE px < 640 DO
            pix32 = 0;

            // iterate over 32 pixels represented by that word
            _32 = 0;
            WHILE _32 < 32 DO
                x = 0;
                y = 0;
                x_2 = 0;
                y_2 = 0;

                // check if calculation escapes boundary
                iteration = 0;
                WHILE iteration < 128 DO
                    y = (((x << 1) * y) >>> 14) + cy;
                    x = ((x_2 - y_2) >>> 14) + cx;

                    x_2 = x * x;
                    y_2 = y * y;

                    // if yes within 128 iterations, make pixel white
                    IF (x_2 + y_2) > 0x40000000 THEN
                        pix32 = pix32 | (1 << _32);
                    END

                    iteration = iteration + 1;
                DONE

                cx = cx + 0x4c;

                _32 = _32 + 1;
            DONE

            framebuffer[((80 * py) + (px >> 3)) >> 2] = pix32;

            px = px + 32;
        DONE
        cy = cy + 0x44;
        py = py + 1;
    DONE
END
